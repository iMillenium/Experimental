//===== Cronus Script =======================================
//= Guias de Morroc
//===== Por: ==================================================
//= L0ne_W0lf
//===== Versão Atual: ========================================
//= 1.3
//===== Descrição: ===========================================
//= [Conversação Oficial]
//= Guias da cidade de Morroc
//===== Comentários Adicionais: ==============================
//= 1.0 Primeira versão, guia da renovação.
//= 1.1 Revisado e Otimizado. [Euphy]
//= 1.2 Atualização do Sistema de Navegação. [Euphy]
//= 1.3 Traduzido para pt-BR [Eski]
//============================================================

morocc,153,286,6	script	Guia#01morocc::GuideMorroc	4_M_MOC_SOLDIER,{
	cutin "moc_soldier",2;
	mes "[Guia de Morroc]";
	mes "Bem-vind"+((Sex) ? "o":"a")+" a ^8B4513^000000, a Cidade do Deserto.";
	mes "Você precisa de ajuda para explorar a cidade?";
	F_Navi("[Guia de Morroc]");
	next;
	while (1) {
		switch(select("[ Principais Lugares ]:[ Mercadores & Ajudantes ]:Remover marcadores do mini-mapa:Cancelar")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Guilda dos Gatunos ]:[ Esfinge ]:[ Oásis ]:[ Forja ]:[ Loja de Armas & Armaduras ]:[ Hotel ]:[ Taverna ]:Menu Anterior")) {
				case 1:
					mes "[Guia de Morroc]";
					mes "A "+F_Navi("Guilda de Gatunos","morocc,26,294","^B9062F")+" está localizada no interior das ^B9062FPirâmides^000000 próximo ao ^B9062FOásis^000000.";
					mes "Gostaria de verificar outras localizações?";
					viewpoint 1,26,294,0,0xFF0000;
					next;
					break;
				case 2:
					mes "[Guia de Morroc]";
					mes "A "+F_Navi("Esfinge","morocc,26,164","^0000FF")+" está em frente ao portão do oeste.";
					mes "Gostaria de verificar outras localizações?";
					viewpoint 1,26,164,1,0x0A82FF;
					next;
					break;
				case 3:
					mes "[Guia de Morroc]";
					mes "Deixe-me marcar para você a localização do "+F_Navi("Oásis","morocc,26,294","^FF5400")+" em seu mini-mapa.";
					mes "Gostaria de verificar outras localizações?";
					viewpoint 1,26,294,2,0xFFB400;
					next;
					break;
				case 4:
					callsub L_Mark, F_Navi("Forja","morocc,47,47","^006400");
					viewpoint 1,47,47,3,0xAAFF00;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("Loja de Armas & Armaduras","morocc,253,56","^7F3300");
					viewpoint 1,253,56,4,0xD2691E;
					next;
					break;
				case 6:
					mes "[Guia de Morroc]";
					mes "Atualmente o "+F_Navi("Hotel","morocc,273,269","^800080")+" está inativo, mas irei marcar sua localização em seu mini-mapa.";
					mes "Gostaria de verificar outras localizações?";
					mes "has been shut down until further notice,";
					viewpoint 1,273,269,5,0xDA70D6;
					next;
					break;
				case 7:
					callsub L_Mark, F_Navi("Taverna","morocc,52,259","^B9062F");
					viewpoint 1,52,259,6,0xFF0000;
					next;
					break;
				case 8:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Funcionárias Kafra ]:[ Vendedora de Ervas Tóxicas ]:[ Criador de Tinturas ]:[ Seiyablem ]:Menu Anterior")) {
				case 1:
					callsub L_Mark,"^0000FFFuncionárias Kafra^000000";
					viewpoint 1,156,97,7,0x0A82FF;
					viewpoint 1,160,258,8,0x0A82FF;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("Vendedora de Ervas Tóxicas","morocc,191,94","^006400");
					viewpoint 1,190,96,9,0xAAFF00;
					next;
					break;
				case 3:
					mes "[Guia de Morroc]";
					mes "O "+F_Navi("Criador de Tinturas","morocc,273,269","^7F3300")+" está atualmente no primeiro andar da pousada.";
					mes "Deixe-me marcar a localização em seu mini-mapa.";
					mes "Gostaria de verificar outras localizações?";
					viewpoint 1,273,269,10,0xD2691E;
					next;
					break;
				case 4:
					mes "[Guia de Morroc]";
					mes F_Navi("Seiyablem","morocc,51,41","^800080")+" é um técnico que pode adicionar ^0000FFslots^000000 em armas e armaduras.";
					mes "Gostaria de verificar outras localizações?";
					viewpoint 1,51,41,11,0xDA70D6;
					next;
					break;
				case 5:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[Guia de Morroc]";
			mes "Claro, vou remover todas as indicações do seu mini-mapa.";
			mes "Há mais algo que eu possa fazer por você?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			viewpoint 2,1,1,10,0xFFFF00;
			viewpoint 2,1,1,11,0xFFFF00;
			next;
			break;
		case 4:
			mes "[Guia de Morroc]";
			mes "Tome cuidado em suas viagens.";
			close2;
			cutin "moc_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[Guia de Morroc]";
	mes "Deixe-me marcar para você a localização para "+getarg(0)+" em seu mini-mapa.";
	mes "Gostaria de verificar outras localizações?";
	return;
}
morocc,154,38,6	duplicate(GuideMorroc)	Guia#02morocc	4_M_MOC_SOLDIER
morocc,296,213,4	duplicate(GuideMorroc)	Guia#03morocc	4_M_MOC_SOLDIER
morocc,28,170,5	duplicate(GuideMorroc)	Guia#04morocc	4_M_MOC_SOLDIER

moc_ruins,159,53,3	script	Guia#01ruins::GuideMocRuins	4_M_MOC_SOLDIER,{
	cutin "moc_soldier",2;
	mes "[Guia de Morroc]";
	mes "Bem-vind"+((Sex) ? "o":"a")+" a ^8B4513Morroc^000000, a Cidade do Deserto.";
	mes "Você precisa de ajuda para explorar a cidade?";
	F_Navi("[Guia de Morroc]");
	next;
	while (1) {
		switch(select("[ Principais Lugares ]:[ Mercadores & Ajudantes ]:Remover marcadores do mini-mapa:Cancelar")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Guilda dos Gatunos ]:[ Esfinge ]:[ Entrada de Morroc ]:Menu Anterior")) {
				case 1:
					mes "[Guia de Morroc]";
					mes "A "+F_Navi("Guilda de Gatunos","moc_ruins,56,161","^B9062F")+" está localizada no interior das ^B9062FPirâmides^000000.";
					mes "Deixe-me marcar sua localização em seu mini-mapa.";
					mes "Gostaria de verificar outras localizações?";
					viewpoint 1,56,161,0,0xFF0000;
					next;
					break;
				case 2:
					mes "[Guia de Morroc]";
					mes "A "+F_Navi("Esfinge","moc_ruins,71,19","^0000FF")+" está em frente à entrada sul do Oasis.";
					mes "Gostaria de verificar outras localizações?";
					viewpoint 1,71,19,1,0x0A82FF;
					next;
					break;
				case 3:
					mes "[Guia de Morroc]";
					mes F_Navi("Morroc","moc_ruins,158,40","^FF5400")+" está a direita da entrada sudeste desse Oásis.";
					mes "Deixe-me marcar sua localização em seu mini-mapa.";
					mes "Gostaria de verificar outras localizações?";
					viewpoint 1,158,40,2,0xFFB400;
					next;
					break;
				case 4:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Oficial do Éden ]:[ Loja de Utilidades ]:[ Funcionária Kafra ]:[ Reparador ]:[ Loja de Bichinhos ]:[ Cozinheiro Auxiliar ]:[ Leablem ]:Menu Anterior")) {
				case 1:
					callsub L_Mark, F_Navi("Oficial do Éden","moc_ruins,68,164","^006400");
					viewpoint 1,68,164,3,0xAAFF00;
					next;
					break;
				case 2:
					mes "[Guia de Morroc]";
					mes "Deixe-me marcar a localização da "+F_Navi("Loja de Utilidades Norte","moc_ruins,92,128","^800080")+" e da "+F_Navi("Loja de Utilidades Sul")+" no seu mini-mapa.";
					mes "Gostaria de checar outras localizações?";
					viewpoint 1,92,128,5,0xDA70D6;
					viewpoint 1,114,63,6,0xDA70D6;
					next;
					break;
				case 3:
					callsub L_Mark, "^B9062FFuncionária Kafra^000000";
					viewpoint 1,59,157,7,0xFF0000;
					next;
					break;
				case 4:
					callsub L_Mark, F_Navi("Reparador","moc_ruins,107,94","^0000FF");
					viewpoint 1,107,94,8,0x0A82FF;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("Loja de Bichinhos","moc_ruins,118,170","^FF5400");
					viewpoint 1,118,170,9,0xFFB400;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("Cozinheiro Auxiliar","moc_ruins,115,123","^006400");
					viewpoint 1,115,123,10,0xAAFF00;
					next;
					break;
				case 7:
					mes "[Guia de Morroc]";
					mes F_Navi("Leablem","moc_ruins,154,86","^7F3300")+" é um técnico que pode adicionar ^0000FFslots^000000 em armas e armaduras.";
					mes "Gostaria de verificar outras localizações?";
					viewpoint 1,154,86,11,0xD2691E;
					next;
					break;
				case 8:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[Guia de Morroc]";
			mes "Claro, vou remover todas as indicações do seu mini-mapa.";
			mes "Há mais algo que eu possa fazer por você?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			viewpoint 2,1,1,10,0xFFFF00;
			viewpoint 2,1,1,11,0xFFFF00;
			next;
			break;
		case 4:
			mes "[Guia de Morroc]";
			mes "Tome cuidado em suas viagens.";
			close2;
			cutin "moc_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[Guia de Morroc]";
	mes "Deixe-me marcar para você a localização para "+getarg(0)+" em seu mini-mapa.";
	mes "Gostaria de verificar outras localizações?";
	return;
}
moc_ruins,70,164,3	duplicate(GuideMocRuins)	Guia#02ruins	4_M_MOC_SOLDIER
moc_ruins,65,44,5	duplicate(GuideMocRuins)	Guia#03ruins	4_M_MOC_SOLDIER
