//===== Cronus Script =======================================
//= Guias de Malaya
//===== Por: ==================================================
//= L0ne_W0lf
//===== Versão Atual: ========================================
//= 1.2
//===== Descrição: ===========================================
//= [Conversação Oficial]
//= Guias da cidade de Malaya
//===== Comentários Adicionais: ==============================
//= 1.0 Primeira versão, guia da renovação.
//= 1.1 Atualização do Sistema de Navegação. [Euphy]
//= 1.2 Traduzido para pt-BR [Eski]
//============================================================

malaya,71,72,4	script	Guia do Porto de Malaya#01::GuideMalaya	4_F_BARYO_WOMAN,{
	mes "[Guia do Porto de Malaya]";
	mes "Bem-vind"+((Sex) ? "o":"a")+" ao ^8B4513Porto de Malaya^000000, o Vilareijo da Respiração.";
	mes "Onde as culturas e lendas ganham vida!";
	mes "Precisa de alguma ajuda para andar pelo vilarejo?";
	F_Navi("[Port Malaya Guide]");
	while(1) {
		next;
		switch(select("[ Funcionárias Kafra ]:[ Hospedaria & Taverna ]:[ Loja de Armas ]:[ Loja de Utilidades ]:[ Hospital ]:[ Parque ]:[ Porto ]:Remover marcadores do mini-mapa.:Cancelar")) {
		case 1:
			callsub L_Mark, "^0000FFFuncionárias Kafra^000000",1;
			viewpoint 1,71,79,0,0x0A82FF;
			viewpoint 1,234,204,1,0x0A82FF;
			break;
		case 2:
			callsub L_Mark, F_Navi("Hospedaria & Taverna","malaya,193,215","^006400");
			viewpoint 1,193,215,2,0xAAFF00;
			break;
		case 3:
			callsub L_Mark, F_Navi("Loja de Armas","malaya,114,212","^008080");
			viewpoint 1,114,212,3,0x4F543C;
			break;
		case 4:
			callsub L_Mark, F_Navi("Loja de Utilidades","malaya,297,167","^FF1493");
			viewpoint 1,297,167,4,0xFF1493;
			break;
		case 5:
			callsub L_Mark, F_Navi("Hospital","malaya,50,75","^8B4513");
			viewpoint 1,50,75,5,0x8B4513;
			break;
		case 6:
			callsub L_Mark, F_Navi("Parque","malaya,280,371","^8B4513");
			viewpoint 1,280,371,6,0x8B4513;
			break;
		case 7:
			callsub L_Mark, F_Navi("Porto","malaya,276,55","^8A2BE2");
			viewpoint 1,276,55,7,0x8A2BE2;
			break;
		case 8:
			mes "[Guia do Porto de Malaya]";
			mes "Irei remover todas as indicações do seu mini-mapa.";
			mes "Há mais algo que eu possa fazer por você?";
			viewpoint 2,1,1,0,0xFFFFFF;
			viewpoint 2,1,1,1,0xFFFFFF;
			viewpoint 2,1,1,2,0xFFFFFF;
			viewpoint 2,1,1,3,0xFFFFFF;
			viewpoint 2,1,1,4,0xFFFFFF;
			viewpoint 2,1,1,5,0xFFFFFF;
			viewpoint 2,1,1,6,0xFFFFFF;
			viewpoint 2,1,1,7,0xFFFFFF;
			break;
		case 9:
			mes "[Port Malaya Guide]";
			mes "Espero que aproveite sua viagem.";
			close;
		}
	}
	end;
L_Mark:
	mes "[Guia do Porto de Malaya]";
	mes "A "+getarg(0)+" "+((getarg(1,0))?"serão":"será");
	mes "marcado"+((getarg(1,0))?"s":"")+" no seu mini-mapa.";
	mes "Cheque seu mini-map.";
	mes "Há mais algo que eu possa fazer por você?";
	return;
}
malaya,250,83,6	duplicate(GuideMalaya)	Guia do Porto de Malaya#02	4_F_BARYO_WOMAN
malaya,224,204,6	duplicate(GuideMalaya)	Guia do Porto de Malaya#03	4_F_BARYO_WOMAN
