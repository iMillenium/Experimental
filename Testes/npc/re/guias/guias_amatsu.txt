//===== Cronus Script ======================================
//= Guias de Amatsu
//===== Por: ===============================================
//= L0ne_W0lf
//===== Versão Atual: ======================================
//= 1.3
//===== Descrição: =========================================
//= [Conversação Oficial]
//= Guias da cidade de Amatsu.
//===== Comentários Adicionais: ============================
//= 1.0 Primeira versão, guia da renovação.
//= 1.1 Adicionado case que faltava. [Euphy]
//= 1.2 Atualização do Sistema de Navegação. [Euphy]
//= 1.3 Traduzido para pt-BR [Eski]
//==========================================================

amatsu,202,91,3	script	Guia de Amatsu#amatsu	4_F_JPN,{
	mes "[Amachang]";
	mes "Bem-vind"+((Sex) ? "o":"a")+" a ^8B4513Amatsu^000000, a cidade dos cidadãos gentis e das belas flores de cerejeiras.";
	next;
	mes "[Amachang]";
	mes "Meu nome é Amachang, sou a 13ª Miss Amatsu.";
	mes "Como uma das minhas funções, eu guiarei-lhe pela cidade.";
	mes "Se você precisar de alguma informação, é só me dizer.";
	F_Navi("[Amachang]");
	while (1) {
		next;
		switch(select("[ Funcionária Kafra ]:[ Bar ]:[ Vendedor de Armas ]:[ Vendedor de Utilidades ]:[ Palácio de Amatsu ]:[ Cozinheiro Auxiliar ]:[ Prédio da Guilda dos Ninjas ]:[ Capitão do Mar ]:Remover indicações do mini-mapa:Cancelar")) {
		case 1:
			callsub L_Mark, "^0000FFFuncionária Kafra^000000";
			viewpoint 1,102,149,0,0x0A82FF;
			break;
		case 2:
			callsub L_Mark, F_Navi("Bar","amatsu,215,116","^006400");
			viewpoint 1,215,116,1,0xAAFF00;
			break;
		case 3:
			callsub L_Mark, F_Navi("Vendedor de Armas","amatsu,129,117","^008080");
			viewpoint 1,129,117,2,0x008080;
			break;
		case 4:
			callsub L_Mark, F_Navi("Vendedor de Utilidades","amatsu,97,117","^FF1493");
			viewpoint 1,97,117,3,0xFF1493;
			break;
		case 5:
			callsub L_Mark, F_Navi("Palácio de Amatsu","amatsu,87,235","^8B4513");
			viewpoint 1,87,235,4,0x8B4513;
			break;
		case 6:
			callsub L_Mark, F_Navi("Cozinheiro Auxiliar","amatsu,206,150","^8A2BE2");
			viewpoint 1,206,150,5,0x8A2BE2;
			break;
		case 7:
			callsub L_Mark, F_Navi("Prédio da Guilda dos Ninjas","amatsu,148,137","^4B0082");
			viewpoint 1,148,137,6,0x4B0082;
			break;
		case 8:
			callsub L_Mark, F_Navi("Capitão do Mar","amatsu,195,79","^00BFFF");
			viewpoint 1,195,79,7,0x00BFFF;
			break;
		case 9:
			mes "[Amachang]";
			mes "Irei remover todas as indicações do seu mini-mapa.";
			mes "Há mais algo que eu possa fazer por você?";
			viewpoint 2,1,1,0,0xFFFFFF;
			viewpoint 2,1,1,1,0xFFFFFF;
			viewpoint 2,1,1,2,0xFFFFFF;
			viewpoint 2,1,1,3,0xFFFFFF;
			viewpoint 2,1,1,4,0xFFFFFF;
			viewpoint 2,1,1,5,0xFFFFFF;
			viewpoint 2,1,1,6,0xFFFFFF;
			viewpoint 2,1,1,7,0xFFFFFF;
			break;
		case 10:
			mes "[Amachang]";
			mes "Aproveite sua viagem~!!";
			close;
		}
	}
	end;
L_Mark:
	mes "[Amachang]";
	mes getarg(0)+" está marcado no seu mini-mapa.";
	mes "Há mais algo que eu possa fazer por você?";
	return;
}
