//===== Cronus Script =======================================
//= Guias de Lutie
//===== Por: ==================================================
//= L0ne_W0lf
//===== Versão Atual: ========================================
//= 1.3
//===== Descrição: ===========================================
//= [Conversação Oficial]
//= Guias da cidade de Lutie
//===== Comentários Adicionais: ==============================
//= 1.0 Primeira versão, guia da renovação.
//= 1.1 Otimizado. [Euphy]
//= 1.2 Atualização do Sistema de Navegação. [Euphy]
//= 1.3 Traduzido para pt-BR [Eski]
//============================================================

xmas,140,137,3	script	Guia de Lutie#xmas	4_F_KID3,{
	mes "[Guia de Lutie]";
	mes "Bem-vind"+((Sex) ? "o":"a")+" à ^8B4513Lutie^000000, a Cidade da Neve.";
	mes "Precisa de alguma ajuda para andar pela cidade?";
	F_Navi("[Lutie Guide]");
	while (1) {
		next;
		switch(select("[ Loja de Presentes ]:[ Igreja ]:[ Loja de Armas & Armaduras ]:[ Loja de Utilidades ]:[ Casa do Papai Noel ]:[ Fábrica de Brinquedos ]:Remover marcadores do mini-mapa:Cancelar")) {
		case 1:
			callsub L_Mark, F_Navi("Loja de Presentes","xmas,172,131","^008080");
			viewpoint 1,172,131,2,0x008080;
			break;
		case 2:
			callsub L_Mark, F_Navi("Igreja","xmas,104,287","^006400");
			viewpoint 1,104,287,1,0xAAFF00;
			break;
		case 3:
			callsub L_Mark, F_Navi("Loja de Armas & Armaduras","xmas,171,158","^FF1493");
			viewpoint 1,171,158,3,0xFF1493;
			break;
		case 4:
			callsub L_Mark, F_Navi("Loja de Utilidades","xmas,122,131","^8B4513");
			viewpoint 1,122,131,4,0x8B4513;
			break;
		case 5:
			callsub L_Mark, F_Navi("Casa do Papai Noel","xmas,149,237","^B9062F"),1;
			viewpoint 1,149,237,5,0x9400D3;
			break;
		case 6:
			callsub L_Mark, F_Navi("Fábrica de Brinquedos","xmas,143,312","^9400D3");
			viewpoint 1,143,312,6,0xFF0000;
			break;
		case 7:
			mes "[Guia de Lutie]";
			mes "Claro, vou remover todas as indicações do seu mini-mapa.";
			mes "Há mais algo que eu possa fazer por você?";
			viewpoint 2,1,1,0,0xFFFFFF;
			viewpoint 2,1,1,1,0xFFFFFF;
			viewpoint 2,1,1,2,0xFFFFFF;
			viewpoint 2,1,1,3,0xFFFFFF;
			viewpoint 2,1,1,4,0xFFFFFF;
			viewpoint 2,1,1,5,0xFFFFFF;
			viewpoint 2,1,1,6,0xFFFFFF;
			break;
		case 8:
			mes "[Guia de Lutie]";
			mes "Esteja segur"+((Sex) ? "o":"a")+" em suas viagens.";
			close;
		}
	}
	end;
L_Mark:
	mes "[Guia de Lutie]";
	mes "Deixe-me marcar a localização para "+getarg(0)+"^000000 em seu mini-mapa.";
	mes "Gostaria de verificar outras localizações?";
	return;
}
