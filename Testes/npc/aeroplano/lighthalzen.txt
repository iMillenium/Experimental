//===== Cronus Script ===========================================
//= Funcionários do Aeroporto de Lighthalzen
//===== Por: ====================================================
//= L0ne_W0lf, Muad_Dib
//===== Descrição: ==============================================
//= Funcionários do Aeroporto de Lighthalzen
//===== Comentários Adicionais: =================================
//= Official NPCs.
//= Traduzido para pt-BR. [Evanio]
//= Revisado de acordo com o bRO. [Portalcake]
//===============================================================

lhz_airport,143,43,5	script	Assistente do Aeroporto#lhz_air1a::lhz_airport1	4_F_01,{
	mes "[Assistente do Aeroporto]";
	mes "Bem-vindo ao aeroporto de Lighthalzen!";
	mes "Aqui você pode ir para Einbroch, Juno ou Hugel!";
	next;
	if (select("Embarcar.:Cancelar.") == 1) {
		mes "[Assistente do Aeroporto]";
		mes "A passagem custa 1.200 zeny.";
		mes "Deseja comprar uma?";
		next;
		if (select("Sim:Não") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1;
				warp "lhz_airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				Zeny -= 1200;
				warp "lhz_airport",148,51;
				end;
			}
			mes "[Assistente do Aeroporto]";
			mes "Me desculpe, mas você não possui 1.200 zeny!";
			mes "Você não pode embarcar.";
			close;
		}
	}
	mes "[Assistente do Aeroporto]";
	mes "Muito obrigada!";
	mes "Volte sempre.";
	close;
}

lhz_airport,158,43,5	duplicate(lhz_airport1)	Assistente do Aeroporto#lhz_air1b	4_F_01
lhz_airport,126,43,5	duplicate(lhz_airport1)	Assistente do Aeroporto#lhz_air1c	4_F_01

lhz_airport,143,49,3	script	Assistente do Desembarque#lhz_air2a::lhz_airport2	4_F_01,{
	mes "[Assistente do Desembarque]";
	mes "Bem-vindo ao Aeroporto de Lighthalzen.";
	mes "Deixe-me te ajudar a chegar no terminal principal!";
	mes "Caso deseje ir para Einbroch, Juno, Rachel, Izlude ou Hugel, por favor embarque novamente no Aeroplano.";
	next;
	if (select("Sair ao terminal.:Cancelar.") == 1) {
		mes "[Assistente do Desembarque]";
		mes "Uma vez no terminal principal, você terá que pagar novamente para embarcar a outra cidade.";
		mes "Apenas saia caso esteja seguro da decisão!";
		mes "Deseja continuar?";
		next;
		if (select("Sim:Não") == 1) {
			warp "lhz_airport",142,40;
			end;
		}
	}
	mes "[Assistente do Desembarque]";
	mes "Muito bem, obrigada pela atenção!";
	mes "Caso deseje voltar, por favor é só me procurar.";
	close;
}

lhz_airport,126,51,3	duplicate(lhz_airport2)	Assistente do Desembarque#lhz_air2b	4_F_01
lhz_airport,158,50,3	duplicate(lhz_airport2)	Assistente do Desembarque#lhz_air2c	4_F_01
