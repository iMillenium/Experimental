//===== Cronus Script ========================================
//= Removedora de Cartas
//===== Por: =================================================
//= TyrNemesis^
//===== Versão atual: ========================================
//= 1.2b
//===== Descrição: ===========================================
//= Remove cartas dos itens equipados.
//===== Comentários adicionais: ==============================
//= 1.0 First version. [TyrNemesis^]
//= 1.2 Optimized and fixed getequipname menu. [Kisuka]
//= 1.2a Added 'disable_items' command. [Euphy]
//= 1.2b Tradução [Millenium]
//============================================================

prt_in,28,73,4	script	Removedora	1_F_ORIENT_04,{

	set .zenycost,200000;	// Definir custo base em zeny do serviço de remoção da carta
	set .percardcost,25000;	// Definir o custo por carta do serviço de remoção de carta
	set .faildestroy,1;	// A removedora de cartas tem chance de fracasso? (os items podem ser destruídos) 1 = Sim, 0 = Não.

	disable_items;
	mes "[Velha Sábia]";
	mes "Bom dia, meu jovem. Eu tenho o poder de remover cartas que você tenha combinado com seu equipamento. Isso pode ajudá-lo?";
	next;
	switch(select("Sim, ajudaria muito.:O que você quer em troca?:Não, Obrigado.")) {
	case 1:
		mes "[Velha Sábia]";
		mes "Muito bem. Qual item devo examinar para você?";
		next;

		setarray .@position$[1], "Cabeça","Armadura","Mão Esquerda","Mão Direita","Capa","Sapatos","Acessorio 1","Acessorio 2","Cabeça 2","Cabeça 3";
		set .@menu$,"";
		for( set .@i,1; .@i <= 10; set .@i,.@i+1 )
		{
			if( getequipisequiped(.@i) )
			set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";

			set .@menu$, .@menu$ + ":";
		}
		set .@part,select(.@menu$);
		if(!getequipisequiped(.@part)) {
			mes "[Velha Sábia]";
			mes "Meu jovem, não tem nada equipado neste local para que eu possa retirar as cartas";
			close;
		}
		if(getequipcardcnt(.@part) == 0) {
			mes "[Velha Sábia]";
			mes "Meu jovem, não há cartas equipadas neste ítem. Não posso trabalhar assim!";
			close;
		}
		set .@cardcount,getequipcardcnt(.@part);
		
		if (!checkweight(1202,(.@cardcount+1))) { 
			mes "^3355FFAguarde um minuto!!";
			mes "Eu não posso oferecer meus";
			mes "serviços a você pois está";
			mes "com excesso de peso";
			mes "ou itens no inventário";
			mes "utilize seu armazém e volte aqui~";
			close;
		}
		mes "[Velha Sábia]";
		mes "Esse item tem " + .@cardcount + " cartas equipadas nele. Para melhorar minha magia, eu preciso de  " + (.zenycost+(.@cardcount * .percardcost)) + " zeny, a ^0000FFFragmento Estelar^000000, e uma ^0000FFGema Amarela^000000.";
		next;
		if(select("Muito bem. Vamos lá.:Não faça isso!") == 2) {
			mes "[Velha Sábia]";
			mes "Muito bem, obrigado por utilizar os meus serviços!";
			close;
		}
		if((Zeny < (.zenycost+(.@cardcount * .percardcost))) || (countitem(1000) < 1) || (countitem(715) < 1)) {
			mes "[Velha Sábia]";
			mes "Você não tem tudo que é necessário para a minha mágica, criança. Volte quando conseguí-los.";
			close;
		}
		mes "[Velha Sábia]";
		mes "Antes de começar, Eu devo lhe avisar que eu posso falhar. Se eu falhar posso destruir suas cartas, os itens ou ambos. Eu não posso lhe devolver nada. Então me diga, o que é mais importante para você? As cartas ou o item?";
		next;
		switch(select("Mudei de idéias sobre isso.:O item.:As Cartas.")) {
		case 1:
			mes "[Velha Sábia]";
			mes "Muito bem. Volte quando precisar dos meus serviços.";
			close;
		case 2:
			set .@failtype,1;
			break;
		case 3:
			set .@failtype,2;
		}
		mes "[Velha Sábia]";
		mes "Muito vem, vamos começar.";
		Zeny -= (.zenycost+(.@cardcount * .percardcost));
		delitem 1000,1;
		delitem 715,1;
		
		// Substituir as constantes nas próximas 3 linhas com chance de falha baseado nos valores definidos em refine_db.txt
		// Primeiro valor = Chance de falha total (Item e Cartas são destruídos)
		// Segundo valor = Chance de falha parcial (Um ou outro será destruído, o jogador decide qual deseja salvar)
		// Terceiro valor = Chance de falha inofenciva (Apenas o seu investimento é perdido)

		set .@failchance,rand(100);
		if (.faildestroy==1) {
			if(.@failchance < 2) {
				next;
				failedremovecards .@part,0;
				mes "[Velha Sábia]";
				mes "O processo foi um fracasso total. Infelizmente o item e as cartas foram destruídas.";
				close;
			}

			if(.@failchance < 8) {
				if (.@failtype == 1) {
					next;
					failedremovecards .@part,1;
					mes "[Velha Sábia]";
					mes "Eu falhei tentando remover as cartas do seu item, suas cartas foram destruídas durante o processo. O item, no entanto, está salvo.";
					close;
				}

				if (.@failtype == 2) {
					next;
					failedremovecards .@part,2;
					mes "[Velha Sábia]";
					mes "Menos mal. Eu tive sucesso em remover as cartas, só que o item foi destruído no processo.";
					close;
				}
			}
		}

		if(.@failchance < 10) {
			next;
			failedremovecards .@part,3;
			mes "[Velha Sábia]";
			mes "Eu falhei tentando remover as cartas. Sorte sua, os ítens e as cartas não sofreram nenhum dano..";
			close;
		}
		next;
		successremovecards .@part;
		mes "[Velha Sábia]";
		mes "O processo foi um grande sucesso. Tome suas cartas e seu item.";
		close;
	case 2:
		mes "[Velha Sábia]";
		mes "Eu preciso de "+.zenycost+" zeny, mais "+.percardcost+" para cada carta removida de seus ítens. E alem disso, preciso de um Fragmento Estelar e uma Gema Amarela para fazer minha magica funcionar.";
		close;
	case 3:
		mes "[Velha Sábia]";
		mes "Muito bem, volte quando precisar dos meus serviços.";
		close;
	}
}
