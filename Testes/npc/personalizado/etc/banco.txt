//===== Cronus Script ========================================
//= Banqueiro 
//===== Por: =================================================
//= Syrus22 (1.0)
//===== Versão atual: ========================================
//= 2.0a
//===== Descrição: ===========================================
//= Permite ao usuário depositar e sacar zenys.
//===== Comentários adicionais: ==============================
//= Syrus22 - There's an optional transaction fee at the top of
//= the script. To use it simply change the first set command
//= to set the cost variable to whatever you want the fee to be.
//= Version 2.0: Optimized and brought the script up to standard. [Jguy]
//= Versão 2.0a: Traduzido. [Portalcake]
//============================================================

prontera,132,217,5	script	Banqueiro	4_M_MANAGER,{
set @cost,0;	// Taxa requerida para realizar uma transação, padrão: 0.
mes "[Banqueiro]";
mes "Bem-vindo ao primeiro banco de Prontera. Como eu posso lhe ajudar?";
next;
switch(select("Eu quero fazer um depósito.:Eu quero fazer um saque.:Qual é o meu saldo?:Cancelar")) {
	case 1:
		mes "[Banqueiro]";
		mes "Muito bem… Qual é a quantia que você deseja depositar? O máximo que você pode depositar em uma única transação é 999.999 Zeny.";
		next;
		if (@cost > 0) {
			mes "[Banqueiro]";
			mes "Devo avisar lhe que há uma taxa de " +@cost + " para realizar transações.";
			next;
		}
		input @deposit;
		if (@deposit < 1) {
			mes "Tenha certeza de que está depositando um valor real.";
			close;
		}
		else if (@deposit > Zeny) {
			mes "Não me parece que você possuí toda essa quantia de zenys que deseja depositar.";
			close;
		}
		else if (@deposit > (Zeny - @cost)) {
			mes "Você precisa de " + @cost + " Zeny para pagar a taxa de transação!";
			close;
		}
		else {
		Zeny -= @deposit;
		Zeny -= @cost;
		set #bankstorage,#bankstorage + @deposit;
		mes "[Banqueiro]";
		mes "Muito obrigado… Os seus zenys estão em boas mãos!";
		close;
		}

	case 2:
		mes "[Banqueiro]";
		mes "Muito bem… Qual é a quantia que você deseja sacar? O máximo que você pode sacar em uma única transação é 999.999 Zeny.";
		next;
		if (@cost > 0) {
			mes "[Banqueiro]";
			mes "Devo avisar lhe que há uma taxa de " +@cost + " para realizar transações.";
			next;
		}
		input @withdrawl;
		if (@withdrawl < 1) {
			mes "Por favor, sem brincadeirinhas. Eu preciso de um valor real para ser sacado.";
			close;
		}
		else if (@withdrawl > #bankstorage) {
			mes "Você tem apenas ^00FF00" + #bankstorage +"^000000 zeny em sua conta!";
			close;
		}
		else if ((@cost > Zeny) && ((Zeny + @withdrawl) > @cost)) {
			mes "[Banqueiro]";
			mes "Você não tem zeny o bastante para pagar a taxa desta transação. Deseja que eu retire o valor da taxa diretamente do saque?";
			next;
			switch(select("Sim, por favor.:Não, obrigado.")){
			case 1:
				mes "[Banqueiro]";
				mes "Removendo " + @cost + " de seu saque para pagar a taxa da transação.";
				set @withdrawl,@withdrawl - @cost;
				set #bankstorage,#bankstorage - @cost;
				set @cost,0;
				next;
				Zeny -= @cost;
				Zeny += @withdrawl;
				set #bankstorage,#bankstorage - @withdrawl;
				mes "[Banqueiro]";
				mes "Aqui estão os seus zenys, tenha um bom dia!";
				close;
			case 2:
				mes "[Banqueiro]";
				mes "Muito bem… Então retorne novamente mais tarde.";
				close;
			}
		}
		else {
			Zeny -= @cost;
			Zeny += @withdrawl;
			set #bankstorage,#bankstorage - @withdrawl;
			mes "[Banqueiro]";
			mes "Aqui estão os seus zenys, tenha um bom dia!";
			close;
		}
	case 3:
		mes "[Banqueiro]";
		mes "Hmmmm… Me deixe checar as papeladas.";
		next;
		mes "* Folheia, Folheia *";
		next;
		mes "[Banqueiro]";
		mes "O seu saldo é de " + #bankstorage + " Zeny.";
		close;
	case 4:
		mes "[Banqueiro]";
		mes "Muito bem… Então retorne novamente mais tarde.";
		close;
	}
}
