//===== Cronus Script ========================================
//= O 2º banco de Prontera (com rendimento diário de 0.01%)
//===== Por: =================================================
//= Lupus (1.0)
//===== Versão atual: ========================================
//= 1.2b
//===== Descrição: ===========================================
//= Um banco que possuí um rendimento em %
//===== Comentários adicionais: ==============================
// A fórmula abaixo explica sobre o rendimento diário.
// #kafrabank/1000 = 0.1% de rendimento por dia.
// #kafrabank/100  =  1%  de rendimento por dia.
// #kafrabank/10   =  10% de rendimento por dia.
// 1.1 Added log of bank operation -> logmes "some info";
// 1.2 Set max income to 100,000z. It would help to avoid
//     zeny exploits when you change DATE at your server
// 1.2a Corrected bad duplicate names. (bugreport:921) [Samuray22]
// 1.2b Traduzido. [Portalcake]
//============================================================

-	script	Banqueiro Clerk::bankg	4_M_ZONDAOYAJI,{
	mes"[Maniss]";
	mes strcharinfo(0)+", seja bem-vindo ao 2º banco de Prontera!";

	set @kb_int,(gettime(6)*31)+gettime(5); //today's number
	set @income,0;
	// Calcular %
	if (#kafrabank<=0 || #kb_int>=@kb_int) goto L_NoIncomeToday;
	set @income,(#kafrabank/1000)*(@kb_int-#kb_int); //@income == % of the sum
	// Constante de rendimento máximo:
	if (@income>100000) set @income,100000;
L_NoIncomeToday:
	set #kb_int,@kb_int; //reset days timer

	if(#kafrabank==0) mes "Nós podemos abrir uma conta para você.";
	if(@income>0) mes "Rendimento de hoje: ^135445" + @income + "^000000 zeny.";
	set #kafrabank,#kafrabank+@income;
	if(#kafrabank>0) mes "Sua conta possuí: ^135445" + #kafrabank + "^000000 zeny.";
	mes "O que você deseja?";
	next;
	if(#kafrabank==0) menu " - Abrir uma conta",-," - Sair",B_EXIT2;
	if(#kafrabank>0) menu " - Depositar dinheiro",-," - Sacar dinheiro",M_WITHDRAW," - Sair",B_EXIT2;

	mes"[Maniss]";
	mes "Por favor, diga-me a quantia em zenys que você deseja depositar.";
	next;
	if(input(@kafrabank) == 1) goto L_TOO_BIG_AMOUNT;

	if(@kafrabank<1000) goto L_LESS_1000;
	if(@kafrabank>Zeny) goto L_NOT_ENOUGH;
	Zeny -= @kafrabank;
	set #kafrabank,#kafrabank+@kafrabank;
	mes"[Maniss]";
	mes "Você fez um depósito de ^135445" + @kafrabank + "z^000000.";
	//we log these zeny operations into the log db
	logmes "Banco %: " + @income +"z, Depósito: "+ @kafrabank +"z, Final: "+ #kafrabank +"z";
	goto B_EXIT;

M_WITHDRAW:
	if(#kafrabank==0) goto L_ZERO_ACCOUNT;
	mes"[Maniss]";
	mes "Sua conta possuí: ^135445" + #kafrabank + "^000000 zeny.";
	mes "Qual é a quantia em zenys que você deseja sacar?";
	next;
	if(input(@kafrabank) == 1) goto L_TOO_BIG_AMOUNT;

	if(@kafrabank<1) goto B_EXIT2;
	if(@kafrabank>#kafrabank) goto L_NOT_ENOUGH;
	set #kafrabank,#kafrabank-@kafrabank;
	Zeny += @kafrabank;
	mes"[Maniss]";
	mes "Aqui estão seus ^135445" + @kafrabank + "z^000000, por favor assine aqui…";
	//we log these zeny operations into the log db
	logmes "Banco %: " + @income +"z, Saque: "+ @kafrabank +"z, Final: "+ #kafrabank +"z";
	goto B_EXIT;

L_NOT_ENOUGH:
	mes"[Maniss]";
	mes "Você não possuí zeny o suficiente para esta operação.";
	next;
	goto B_EXIT2;

L_ZERO_ACCOUNT:
	mes"[Maniss]";
	mes "Você não possuí nenhum zeny em sua conta.";
	next;
	goto B_EXIT2;

L_TOO_BIG_AMOUNT:
	mes"[Maniss]";
	mes "Desculpe. O máximo que você pode depositar em uma transação é 10.000.000 zeny.";
	next;
	goto B_EXIT2;

L_LESS_1000:
	mes"[Maniss]";
	mes "Desculpe, mas o valor mínimo que você pode depositar é 1.000 zeny.";
	next;
	goto B_EXIT2;

B_EXIT:
	mes "Muito bom… Sua operação foi realizada com sucesso!";
	next;

B_EXIT2:
	mes"[Maniss]";
	mes "Muito obrigado por usar o nosso serviço bancário, nós esperamos vê-lo novamente em breve.";
	close;
}

prontera,131,190,1	duplicate(bankg)	Banqueiro Clerk#1-1	4_M_ZONDAOYAJI
geffen,125,73,3	duplicate(bankg)	Banqueiro Clerk#2-1	4_M_ZONDAOYAJI
izlude,145,107,1	duplicate(bankg)	Banqueiro Clerk#3-1	4_M_ZONDAOYAJI
morocc,147,84,7	duplicate(bankg)	Banqueiro Clerk#4-1	4_M_ZONDAOYAJI
