//===== Cronus Script ========================================
//= Vinculador de Itens
//===== Por: =================================================
//= Akinari
//===== Compatível com: ======================================
//= Revision 17351+ (rAthena)
//= Revision 12949+ (Hercules)
//= Cronus-Emulator
//===== Descrição: ===========================================
//= Permite o usuário pagar um preço para vincular o item á
//= Conta, personagem ou clã.
//= Tradução [Millenium]
//============================================================

prontera,144,174,4	script	Vinculador de Itens	4_M_JP_MID,{

	mes "Eu posso vincular seus itens á sua conta, clã ou personagem"+((.bindprice)?" por apenas ^0000FF"+.bindprice+"^000000 ":"")+".";
	next;
	mes "Com isso, você pode ter certeza que seus itens ficarão seguros.";
	next;
	mes "O que você gostaria de fazer?";
	if(select("Vincular:Desvincular") == 1) {
		if(Zeny < .bindprice) {
			mes "Você não possuí zeny suficiente para vincular este item.";
			close;
		}
		mes "Que tipo de vinculo?";
		.@boundtype = 1 << (select("Conta:Clã:Personagem")-1);
		if(.@boundtype == 2 && (!getcharid(2) || getguildmaster(getcharid(2)) != strcharinfo(0))) {
			mes "Para vincular um item ao clã, você deve ser o lider do clã.";
			close;
		}
		getinventorylist();
		for(.@i = 0; .@i < @inventorylist_count; .@i++) {
			// Mostraremos apenas os itens que podem ser vinculados.
			// Todos os equipamentos por padrão.
			if(@inventorylist_bound[.@i])
				continue;
			if(((.allowbind & 1) && (getiteminfo(@inventorylist_id[.@i],2) == (4|5))) || 
				((.allowbind & 2) && (getiteminfo(@inventorylist_id[.@i],2) == (0|2|11|18))) ||
				((.allowbind & 4) && (getiteminfo(@inventorylist_id[.@i],2) == (3|6|7|8|10)))
			) {
				set .@bindlist$, .@bindlist$ + ":" + getitemname(@inventorylist_id[.@i]) + " - " + @inventorylist_id[.@i];
				set .@bindlist[.@j],.@i;
				.@j++;
			}
		}
		.@item = .@bindlist[select(.@bindlist$)-2];
		next;
		mes "Antes de continuar, eu preciso que você saiba que eu não consigo dizer a diferença entre varios itens iguais. Se você tem um item especifico que deseja vincular, por favor remova todas as duplicatas do seu inventario.";
		if(select("Eu entendi, continue:Espere um minuto") == 2) {
			next;
			mes "Eu estarei aqui quando você estiver pronto.";
			close;
		}
		next;
		mes "Tem certeza que deseja vincular sua "+ getitemname(@inventorylist_id[.@item]) +" para o seu "+.boundtypes$[.@boundtype]+"?";
		if(select("Sim:Não") == 1) {
			Zeny -= .bindprice;
			delitem2 @inventorylist_id[.@item],@inventorylist_amount[.@item],@inventorylist_identify[.@item],@inventorylist_refine[.@item],@inventorylist_attribute[.@item],@inventorylist_card1[.@item],@inventorylist_card2[.@item],@inventorylist_card3[.@item],@inventorylist_card4[.@item];
			getitembound2 @inventorylist_id[.@item],@inventorylist_amount[.@item],@inventorylist_identify[.@item],@inventorylist_refine[.@item],@inventorylist_attribute[.@item],@inventorylist_card1[.@item],@inventorylist_card2[.@item],@inventorylist_card3[.@item],@inventorylist_card4[.@item],.@boundtype;
			mes "Tudo pronto!";
			if(.logbinds)
				logmes "Vinculado "+ @inventorylist_amount[.@item]+" "+@inventorylist_id[.@item]+" como "+.boundtypes$[.@boundtype]+" tipo.";
		}
	} else {
		if(!countbound()) {
			mes "Não há nenhum item vinculado em seu inventário. Não há muito que eu possa fazer aqui.";
			close;
		}
		countbound(2);
		if(.unbindprice) {
			mes "Desvincular um item tem uma taxa de ^0000FF"+.unbindprice+"^000000 zeny.";
			if(Zeny < .unbindprice) {
				mes "Você não possuí zeny suficiente para desvincular um item";
				close;
			}
		}
		getinventorylist();
		for(.@i = 0; .@i < @inventorylist_count; .@i++) {
			if(@inventorylist_bound[.@i]) {
				set .@bindlist$, .@bindlist$ + ":" + getitemname(@inventorylist_id[.@i]) + " - " + @inventorylist_id[.@i];
				set .@bindlist[.@j],.@i;
				.@j++;
			}
		}
		.@item = .@bindlist[select(.@bindlist$)-2];
		next;
		for(.@i = 0; .@i < getarraysize(@bound_items); .@i++) {
			if(@inventorylist_id[.@item] == @bound_items[.@i] &&
				(!getcharid(2) || getguildmaster(getcharid(2)) != strcharinfo(0))
			) {
				mes "Só desvinculo o item de um clã á pedido do mestre do clã, obrigado.";
				close;
			}
		}
		mes "Antes de continuar, eu preciso que você saiba que eu não consigo dizer a diferença entre varios itens iguais. Se você tem um item especifico para meu trabalho, por favor remova todas as duplicatas do seu inventario.";
		if(select("Eu entendo, continue:Espere um minuto") == 2) {
			next;
			mes "Eu estarei aqui quando você estiver pronto";
			close;
		}
		next;
		mes "Você tem certeza que deseja desvincular o seu "+ getitemname(@inventorylist_id[.@item]) +"?";
		if(select("Sim:Não") == 1) {
			Zeny -= .unbindprice;
			delitem2 @inventorylist_id[.@item],@inventorylist_amount[.@item],@inventorylist_identify[.@item],@inventorylist_refine[.@item],@inventorylist_attribute[.@item],@inventorylist_card1[.@item],@inventorylist_card2[.@item],@inventorylist_card3[.@item],@inventorylist_card4[.@item];
			getitem2 @inventorylist_id[.@item],@inventorylist_amount[.@item],@inventorylist_identify[.@item],@inventorylist_refine[.@item],@inventorylist_attribute[.@item],@inventorylist_card1[.@item],@inventorylist_card2[.@item],@inventorylist_card3[.@item],@inventorylist_card4[.@item];
			mes "All done!";
			if(.logbinds)
				logmes "Desvinculado "+ @inventorylist_amount[.@item]+" "+@inventorylist_id[.@item]+".";
		}
	}
	close;

OnInit:
	//* Configurações *\\
	//Preço
	.bindprice = 0;
	.unbindprice = 100000;

	// O que pode ser vinculado?
	// Some se for necessário, exemplo 1+2 = 3
	// O que permite que equipamentos e consumíveis sejam vinculados.
	// 1 = Equipamento - 2 = Consumíveis - 4 = Etc
	.allowbind = 1;

	// Registar logs de vinculamento via NPC?
	.logbinds = 1;

	// Outros
	.boundtypes$[1] = "conta";
	.boundtypes$[2] = "clã";
	.boundtypes$[4] = "personagem";
	end;
}
