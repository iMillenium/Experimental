//===== Cronus Script ========================================
//= Estilista
//===== Por: =================================================
//= Euphy
//===== Versão atual: ========================================
//= 1.1a
//===== Descrição: ===========================================
//= Altera seu estilo de cabelo, cor do cabelo e cor de roupa.
//===== Comentários adicionais:===============================
//= 1.1 Switched to 'getbattleflag', credits to Saithis. [Euphy]
//= 1.1a Tradução [ Millenium ]
//============================================================

prontera,170,180,1	script	Estilista#custom_Estilista	2_M_DYEINGER,{

	setarray .@styles[1],getbattleflag("max_cloth_color"),getbattleflag("max_hair_style"),getbattleflag("max_hair_color");
	setarray .@Look[1],7,1,6;
	set .@s, select(" ~ Cor da roupa: ~ Estilo do cabelo: ~ Cor do cabelo");
	set .@Revert, getlook(.@Look[.@s]);
	set .@style,1;
	while(1) {
		setlook .@Look[.@s], .@style;
		message strcharinfo(0),"Este é o estilo #"+.@style+".";
		set .@menu$, " ~ Próximo (^0055FF"+((.@style!=.@styles[.@s])?.@style+1:1)+"^000000): ~ Anterior (^0055FF"+((.@style!=1)?.@style-1:.@styles[.@s])+"^000000): ~ Vá para...: ~ Voltar para original (^0055FF"+.@Revert+"^000000)";
		switch(select(.@menu$)) {
			case 1: set .@style, ((.@style!=.@styles[.@s])?.@style+1:1); break;
			case 2: set .@style, ((.@style!=1)?.@style-1:.@styles[.@s]); break;
			case 3: message strcharinfo(0),"Escolha entre 1 - "+.@styles[.@s]+".";
				input .@style,0,.@styles[.@s];
				if (!.@style) set .@style, rand(1,.@styles[.@s]);
				break;
			case 4: set .@style, .@Revert; setlook .@Look[.@s], .@Revert; break;
		}
	}
}
