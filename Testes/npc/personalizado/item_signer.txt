//===== Cronus Script ========================================
//= Assine seus itens.
//===== Por: =================================================
//= Lupus
//===== Versão atual: ========================================
//= 1.1b
//===== Descrição: ===========================================
//= Escreva seu nome em seu equipamento raro ou arma. ^_-
//===== Comentários adicionais: ==============================
//= 1.1 Cleaned and edited for general use. [Euphy]
//= 1.1a Added 'disable_items' command. [Euphy]
//= 1.1b Tradução [Millenium]
//============================================================

prt_in,24,61,7	script	Perchik	1_M_01,{
	
	setarray .@item[0],644,3;	// Item requerido: <ID>,<quantidade>{,...} (0: desativa)
	setarray .@cost[0],0,5000;	// Zeny requerido: <preço base>,<preço por serviço>

	disable_items;
	mes "[Perchik]";
	mes "Eu posso ^0055FFassinar seu nome^000000 em praticamente qualquer item raro que você possua.";
	next;
	if(select("Conte-me mais...:Assine meu item, por favor!") == 1) {
		mes "[Perchik]";
		mes "Eu posso colocar seu nome em qualquer slot de equipamente ou arma.";
		emotion e_ic;
		next;
		mes "[Perchik]";
		if (getarraysize(.@item) || getarraysize(.@cost)) {
			mes "Pelo meu trabalho eu aceito:";
			if (getarraysize(.@item)) for(set .@i,0; .@i<getarraysize(.@item); set .@i,.@i+2)
				mes " ~ "+.@item[.@i+1]+"x "+getitemname(.@item[.@i]);
			if (.@cost[0]) mes " ~ "+.@cost[0]+" Zeny";
			if (.@cost[1]) mes " ~ "+.@cost[1]+" Zeny por refino";
		}
		else mes "Eu posso trabalhar de graça, porém...";
		next;
		emotion e_cry;
		mes "[Perchik]";
		mes "Eu tenho 12 crianças famintas";
		mes "e uma esposa muito brava.";
		next;
		mes "[Perchik]";
		mes "Ou era 12 crianças muito bravas";
		mes "e uma esposa faminta...";
		emotion e_hmm;
		close;
	}
	mes "[Perchik]";
	mes "Me mostre o item que deseja assinar...";
	next;
	setarray .@pos$[1],"Cabeça (Topo)","Armadura","Mão Esquerda","Mão Direita","Armadura","Sapatos","Acessório (Esquerda)","Acessório (Direita)","Cabeça (Meio)","Cabeça (Baixo)";
	set .@menu$,"";
	for (set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@i))
			set .@menu$, .@menu$+.@pos$[.@i]+" [^0055FF"+getequipname(.@i)+"^000000]";
		set .@menu$, .@menu$+":";
	}
	set .@part, select(.@menu$);
	set .@id, getequipid(.@part);
	set .@ref, getequiprefinerycnt(.@part);
	mes "[Perchik]";
	if (!getequipisequiped(.@part)) {
		mes "Não há nada equipado aqui!";
		emotion e_wah;
		close;
	}
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		set .@slot[.@i], getequipcardid(.@part,.@i);
	if (.@slot[0]==255 || .@slot[0]==254 || .@slot[0]<0) {
		mes "Opa, este item já está assinado.";
		mes "Eu nunca tocaria no trabalho de um mestre.";
		emotion e_hmm;
		close;
	}
	if (.@slot[0]>4000 && .@slot[0]<5000) {
		mes "Uma carta? Aqui?!";
		mes "Como eu disse antes, eu não assino itens com cartas.";
		emotion e_hmm;
		close;
	}
	if (getiteminfo(.@id,10)) {
		mes "Desculpe, eu não assino itens com slot.";
		emotion e_sry;
		close;
	}
	getinventorylist;
	for(set .@i,0; .@i<@inventorylist_count; set .@i,.@i+1)
		if (@inventorylist_expire[.@i] != 0) {
			mes "Desculpe, eu não assino itens alugados!";
			emotion e_hmm;
			close;
		}
	set .@price, .@cost[0]+(.@cost[1]*.@ref);
	if (getarraysize(.@item) || .@price) {
		mes "Eu vou precisar de:";
		if (getarraysize(.@item)) for(set .@i,0; .@i<getarraysize(.@item); set .@i,.@i+2) {
			mes " ~ "+.@item[.@i+1]+"x "+getitemname(.@item[.@i]);
			if (countitem(.@item[.@i]) < .@item[.@i+1]) set .@nr,1;
		}
		if (.@price) mes " ~ "+.@price+" Zeny";
	}
	mes "Devo assinar seu ^0055FF"+getitemname(.@id)+"^000000?";
	next;
	if(select("Sim!:Não, mudei de idéia")==2) {
		mes "[Perchik]";
		mes "Vejo você por ai...";
		emotion e_yawn;
		close;
	}
	mes "[Perchik]";
	if (Zeny < .@price || .@nr) {
		mes " Agradecimentos não pagam pelo meu trabalho.";
		emotion e_sry;
		close;
	}
	if (!countitem2(.@id,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3])) {
		mes "Onde está o "+getitemname(@id)+"...?";
		npctalk "Você é um pequeno trapaceiro!";
		logmes "TRAPACEIRO: Tentou me fazer assinar um item que você não tem: "+getitemname(@id);
		emotion e_wah;
		close;
	}
	if (.@price) Zeny -= .@price;
	if (getarraysize(.@item))  for(set .@i,0; .@i<getarraysize(.@item); set .@i,.@i+2)
		delitem .@item[.@i], .@item[.@i+1];
	delitem2 .@id,1,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3];
	mes "Pronto!";
	emotion e_proud;
	getitem2 .@id,1,1,.@ref,0,254,0,getcharid(0)&0xffff,(getcharid(0)>>16)&0xffff;
	equip .@id;
	close;
}
