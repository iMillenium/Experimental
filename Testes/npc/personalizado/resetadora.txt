//===== Cronus Script ========================================
//= Resetadora
//===== Por: =================================================
//= Hercules Dev Team
//===== Versão atual: ========================================
//= 1.3a
//===== Descrição: ===========================================
//= Reseta habilidades, atributos ou ambos.
//===== Comentários Adicionais: ==============================
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning [Euphy]
//= 1.3 All statuses removed upon skill reset. [Euphy]
//= 1.3a Tradução [Millenium & Portalcake]
//============================================================

prontera,150,193,4	script	Resetadora	4_F_TELEPORTER,{

	set .@ResetStat,5000;	// Custo para resetar atributos.
	set .@ResetSkill,5000;	// Custo para resetar habilidades.
	set .@ResetBoth,9000;	// Custo para resetar ambos.

	mes "[Resetadora]";
	mes "Eu sou resetadora.";
	mes "Reseto Status por: "+ .@ResetStat +"z";
	mes "Reseto Habilidades por: "+ .@ResetSkill +"z";
	mes "Reseto Ambos por: "+ .@ResetBoth +"z";
	mes "Por favor, selecione o que deseja:";
	next;
	switch(select("^FF3355Resetar Habilidades:Resetar Atributos:Resetar Ambos^000000:Cancelar")) {
	case 1:
		mes "[Resetadora]";
		if (Zeny < .@ResetSkill) {
			mes "Desculpe, você não tem dinheiro suficiente.";
			close;
		}
		Zeny -= .@ResetSkill;
		sc_end SC_ALL;
		resetskill;
		mes "Suas habilidades foram resetadas com sucesso.";
		close;
	case 2:
		mes "[Resetadora]";
		if (Zeny < .@ResetStat) {
			mes "Desculpe, você não tem dinheiro suficiente.";
			close;
		}
		Zeny -= .@ResetStat;
		resetstatus;
		mes "Seus atributos foram resetados com sucesso.";
		close;
	case 3:
		mes "[Resetadora]";
		if (Zeny < .@ResetBoth) {
			mes "Desculpe, você não tem dinheiro suficiente.";
			close;
		}
		Zeny -= .@ResetBoth;
		sc_end SC_ALL;
		resetskill;
		resetstatus;
		mes "Seus atributos e habilidades foram resetados com sucesso.";
		close;
	case 4:
		close;
	}
}
